#define AIRLOCK_CONTROL_RANGE 22

// This code allows for airlocks to be controlled externally by setting an id_tag and comm frequency (disables ID access)
/obj/machinery/door/airlock
	var/id_tag
	var/shockedby = list()
	var/cur_command = null	//the command the door is currently attempting to complete

/obj/machinery/door/airlock/process()
	if(arePowerSystemsOn() && cur_command)
		execute_current_command()
	else
		return PROCESS_KILL

/obj/machinery/door/airlock/receive_signal(datum/signal/signal)
	if(!arePowerSystemsOn())
		return //no power

	if(!signal || signal.encryption)
		return

	if(id_tag != signal.data["tag"] || !signal.data["command"])
		return

	cur_command = signal.data["command"]
	spawn()
		execute_current_command()

/obj/machinery/door/airlock/proc/execute_current_command()
	if(operating || emagged)
		return //emagged or busy doing something else

	if(!cur_command)
		return

	do_command(cur_command)
	if(command_completed(cur_command))
		cur_command = null
	else
		START_PROCESSING(SSmachines, src)

/obj/machinery/door/airlock/proc/do_command(command)
	switch(command)
		if("open")
			open()

		if("close")
			close()

		if("unlock")
			unlock()

		if("lock")
			lock()

		if("secure_open")
			unlock()

			sleep(2)
			open()

			lock()

		if("secure_close")
			unlock()
			close()

			lock()
			sleep(2)

	send_status()

/obj/machinery/door/airlock/proc/command_completed(command)
	switch(command)
		if("open")
			return (!density)

		if("close")
			return density

		if("unlock")
			return !locked

		if("lock")
			return locked

		if("secure_open")
			return (locked && !density)

		if("secure_close")
			return (locked && density)

	return 1	//Unknown command. Just assume it's completed.

/obj/machinery/door/airlock/proc/send_status(bumped = 0)
	if(radio_connection)
		var/datum/signal/signal = new
		signal.transmission_method = 1 //radio signal
		signal.data["tag"] = id_tag
		signal.data["timestamp"] = world.time

		signal.data["door_status"] = density?("closed"):("open")
		signal.data["lock_status"] = locked?("locked"):("unlocked")

		if(bumped)
			signal.data["bumped_with_access"] = 1

		radio_connection.post_signal(src, signal, range = AIRLOCK_CONTROL_RANGE, filter = RADIO_AIRLOCK)

/obj/machinery/door/airlock/open(surpress_send)
	. = ..()
	if(!surpress_send) send_status()

/obj/machinery/door/airlock/close(surpress_send)
	. = ..()
	if(!surpress_send) send_status()

/obj/machinery/door/airlock/Bumped(atom/movable/moving_atom)
	..(moving_atom)
	if(ismecha(moving_atom))
		var/obj/mecha/mecha = moving_atom
		if(density && radio_connection && mecha.occupant && (allowed(mecha.occupant) || check_access_list(mecha.operation_req_access)))
			send_status(1)
	return

/obj/machinery/door/airlock/set_frequency(new_frequency)
	SSradio.remove_object(src, frequency)
	if(new_frequency)
		frequency = new_frequency
		radio_connection = SSradio.add_object(src, frequency, RADIO_AIRLOCK)

/obj/machinery/door/airlock/Initialize()
	..()
	if(frequency)
		set_frequency(frequency)


/obj/machinery/door/airlock/New()
	..()

	if(SSradio)
		set_frequency(frequency)

/obj/machinery/airlock_sensor
	icon = 'icons/obj/machines/airlock_machines.dmi'
	icon_state = "airlock_sensor_off"
	name = "airlock sensor"
	anchored = TRUE
	resistance_flags = FIRE_PROOF
	power_channel = ENVIRON

	var/id_tag
	var/master_tag
	frequency = 1379
	var/command = "cycle"

	var/on = 1
	var/alert = 0
	var/previousPressure

/obj/machinery/airlock_sensor/update_icon_state()
	if(on)
		if(alert)
			icon_state = "airlock_sensor_alert"
		else
			icon_state = "airlock_sensor_standby"
	else
		icon_state = "airlock_sensor_off"

/obj/machinery/airlock_sensor/attack_hand(mob/user)
	add_fingerprint(user)
	var/datum/signal/signal = new
	signal.transmission_method = 1 //radio signal
	signal.data["tag"] = master_tag
	signal.data["command"] = command

	radio_connection.post_signal(src, signal, range = AIRLOCK_CONTROL_RANGE, filter = RADIO_AIRLOCK)
	flick("airlock_sensor_cycle", src)

/obj/machinery/airlock_sensor/process()
	if(on)
		var/datum/gas_mixture/air_sample = return_air()
		var/pressure = round(air_sample.return_pressure(),0.1)

		if(abs(pressure - previousPressure) > 0.001 || previousPressure == null)
			var/datum/signal/signal = new
			signal.transmission_method = 1 //radio signal
			signal.data["tag"] = id_tag
			signal.data["timestamp"] = world.time
			signal.data["pressure"] = num2text(pressure)

			radio_connection.post_signal(src, signal, range = AIRLOCK_CONTROL_RANGE, filter = RADIO_AIRLOCK)

			previousPressure = pressure

			alert = (pressure < ONE_ATMOSPHERE*0.8)

			update_icon(UPDATE_ICON_STATE)

/obj/machinery/airlock_sensor/set_frequency(new_frequency)
	SSradio.remove_object(src, frequency)
	frequency = new_frequency
	radio_connection = SSradio.add_object(src, frequency, RADIO_AIRLOCK)

/obj/machinery/airlock_sensor/Initialize()
	..()
	set_frequency(frequency)

/obj/machinery/airlock_sensor/New()
	..()
	if(SSradio)
		set_frequency(frequency)

/obj/machinery/airlock_sensor/Destroy()
	if(SSradio)
		SSradio.remove_object(src, frequency)
	radio_connection = null
	return ..()

/obj/machinery/airlock_sensor/airlock_interior
	command = "cycle_interior"

/obj/machinery/airlock_sensor/airlock_exterior
	command = "cycle_exterior"

/obj/machinery/access_button
	icon = 'icons/obj/machines/airlock_machines.dmi'
	icon_state = "access_button_standby"
	name = "access button"
	anchored = TRUE
	power_channel = ENVIRON

	var/master_tag
	frequency = AIRLOCK_FREQ
	var/command = "cycle"
	var/on = 1
	var/wires = 3
	/*
	Bitflag,	1=checkID
				2=Network Access
	*/

/obj/machinery/access_button/update_icon_state()
	if(on)
		icon_state = "access_button_standby"
	else
		icon_state = "access_button_off"

/obj/machinery/access_button/attack_ai(mob/user)
	if(wires & 2)
		return ..(user)
	else
		to_chat(user, "Error, no route to host.")

/obj/machinery/access_button/attackby(obj/item/I, mob/user, params)
	//Swiping ID on the access button
	if(I.GetID())
		attack_hand(user)
		return
	return ..()

/obj/machinery/access_button/attack_ghost(mob/user)
	if(user.can_advanced_admin_interact())
		return attack_hand(user)

/obj/machinery/access_button/attack_hand(mob/user)
	add_fingerprint(usr)

	if(!allowed(user) && (wires & 1) && !user.can_advanced_admin_interact())
		to_chat(user, span_warning("Access denied."))
		playsound(src, pick('sound/machines/button.ogg', 'sound/machines/button_alternate.ogg', 'sound/machines/button_meloboom.ogg'), 20)

	else if(radio_connection)
		var/datum/signal/signal = new
		signal.transmission_method = 1 //radio signal
		signal.data["tag"] = master_tag
		signal.data["command"] = command

		radio_connection.post_signal(src, signal, range = AIRLOCK_CONTROL_RANGE, filter = RADIO_AIRLOCK)
	flick("access_button_cycle", src)

/obj/machinery/access_button/set_frequency(new_frequency)
	SSradio.remove_object(src, frequency)
	frequency = new_frequency
	radio_connection = SSradio.add_object(src, frequency, RADIO_AIRLOCK)

/obj/machinery/access_button/Initialize()
	..()
	set_frequency(frequency)

/obj/machinery/access_button/New()
	..()

	if(SSradio)
		set_frequency(frequency)

/obj/machinery/access_button/Destroy()
	if(SSradio)
		SSradio.remove_object(src, frequency)
	radio_connection = null
	return ..()

/obj/machinery/access_button/airlock_interior
	frequency = 1379
	command = "cycle_interior"

/obj/machinery/access_button/airlock_exterior
	frequency = 1379
	command = "cycle_exterior"
